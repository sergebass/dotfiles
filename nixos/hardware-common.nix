# NixOS hardware configuration shared across multiple systems.

{ config, lib, pkgs, modulesPath, ... }: let
  btrfsListFailedInodesScript = with pkgs; writeShellScriptBin "btrfs-list-failed-inodes" ''
    dmesg | grep -Po 'csum failed root \d+ ino\S* \d+' | awk '{print $6}' | sort -nu
  '';

  btrfsListFailedFilesScript = with pkgs; writeShellScriptBin "btrfs-list-failed-files" ''
    # $1: root of the filesystem/directory tree where the failed files are searched

dmesg | grep -Po 'csum failed root \d+ ino\S* \d+' | awk '{print $6}' | sort -nu | xargs -n 1 find $1 -inum 2> /dev/null
  '';

in {
  imports = [
     (modulesPath + "/installer/scan/not-detected.nix")  # Generated by nixos-generate-config
  ];

  hardware = {
    bluetooth = {
      enable = true;
      powerOnBoot = true;
      package = pkgs.bluez;
    };
  };

  boot = {
    kernel.sysctl = {
      "kernel.sysrq" = 1;  # Enable all SysRq functions
    };

    kernelPackages = pkgs.linuxPackages;

    tmp.useTmpfs = true;  # Save SSD from some wear and tear

    supportedFilesystems = [
      "btrfs"
      "zfs"
      "exfat"
      "ntfs"
      "ntfs3"
    ];

    zfs = {
      forceImportRoot = false;
    };
  };

  services = {
    udev.enable = true;

    btrfs = {
      autoScrub = {
        enable = true;  # Make use of btrfs scrub to check data integrity periodically
        interval = "monthly";  # See `man 7 systemd.time` for possible values
        fileSystems = [ "/" ];
      };
    };

    udisks2 = {
      enable = true;  # For automounting external drives in GUI file managers
      settings = {
        "udisks2.conf" = {
          defaults = {
            encryption = "luks2";
          };
          udisks2 = {
            modules = [
              "*"
            ];
            modules_load_preference = "ondemand";
          };
        };

        "mount_options.conf" = {
          defaults = {
            defaults = "noatime";
            btrfs_defaults = "compress=zstd,noatime";
          };
        };
      };
    };

    pulseaudio.enable = false;  # Use PipeWire instead

    pipewire = {
      enable = true;
      alsa.enable = true;
      alsa.support32Bit = true;
      pulse.enable = true;  # Emulate PulseAudio
      jack.enable = true;
    };
  };

  environment = {
    # Tools used for hardware management and inspection
    systemPackages = with pkgs; [
      alsa-utils  # ALSA, the Advanced Linux Sound Architecture utils
      btrfs-heatmap  # Visualize the layout of a mounted btrfs
      btrfs-progs  # Utilities for the btrfs filesystem
      btrfs-snap  # Create and maintain the history of snapshots of btrfs filesystems
      compsize  # Compression ratio calculator for btrfs
      cryptsetup  # LUKS for dm-crypt
      dmidecode  # Tool that reads information about your system's hardware from the BIOS according to the SMBIOS/DMI standard
      exfatprogs  # exFAT filesystem userspace utilities
      fastfetch  # Like neofetch, but much faster because written in C
      fq  # jq for binary formats
      hddtemp  # HDD temperature measurement tools
      hdparm  # Tool to get/set ATA/SATA drive parameters under Linux
      hwinfo  # Hardware detection tool from openSUSE
      lm_sensors  # Onboard sensor measurement tools
      lshw  # Provide detailed information on the hardware configuration of the machine
      parted  # Create, destroy, resize, check, and copy partitions
      pciutils  # A collection of programs for inspecting and manipulating configuration of PCI devices
      simple-mtpfs  # Simple MTP fuse filesystem driver (for Android phones, Garmin watches etc.)
      tinyxxd  # Drop-in replacement and standalone version of the hex dump utility that comes with ViM
      usbutils  # Tools for working with USB devices, such as lsusb
      xcd  # Colorized hexdump tool
      zfs  # ZFS Filesystem Linux Userspace Tools
    ] ++ [
      # Our custom scripts for managing hardware, filesystems etc.
      btrfsListFailedInodesScript
      btrfsListFailedFilesScript
    ];
  };
}
